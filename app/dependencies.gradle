def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def local = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

def di = dependencyGroup {
    def version = '2.2.1'
    implementation "org.koin:koin-android:$version"
    implementation "org.koin:koin-android-viewmodel:$version"
}

def android = dependencyGroup {
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation "com.android.support:multidex:1.0.3"
}

def network = dependencyGroup {
    def version = '2.8.1'
    implementation "com.squareup.retrofit2:retrofit:$version"
    implementation "com.squareup.retrofit2:converter-gson:$version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'
    implementation 'org.conscrypt:conscrypt-android:2.2.1'  //To remove warnings
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.8.1'
}

def rx = dependencyGroup {
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

def test = dependencyGroup {
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'io.mockk:mockk:1.9.3.kotlin12'
    androidTestImplementation "io.mockk:mockk-android:1.9.3.kotlin12"
    testImplementation 'com.jraska.livedata:testing-ktx:1.1.2'
    implementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    testImplementation "org.koin:koin-test:2.1.6"
    def fragment_version = "1.2.3"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"

    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
}

def room = dependencyGroup {
    def room_version = "2.2.5"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "net.zetetic:android-database-sqlcipher:4.4.2"
    implementation "androidx.sqlite:sqlite:2.0.1"
}

dependencies {
    local()
    android()
    di()
    network()
    rx()
    test()
    room()
}